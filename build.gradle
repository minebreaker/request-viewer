plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'maven-publish'
    id 'jacoco'
}

group 'rip.deadcode.request-viewer'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = "https://maven.pkg.github.com/minebreaker/Izvestia"
        credentials {
            username = project.hasProperty("GITHUB_USER") ? GITHUB_USER : ''
            password = project.hasProperty("GITHUB_TOKEN") ? GITHUB_TOKEN : ''
        }
    }
}

dependencies {
    implementation(
            'org.jetbrains.kotlin:kotlin-stdlib',
            'org.eclipse.jetty:jetty-server:11.0.6',
            'com.google.guava:guava:31.0.1-jre',
            'com.google.code.findbugs:jsr305:3.0.2',
//            'com.google.code.gson:gson:2.8.7',
//            'com.google.http-client:google-http-client:1.39.2',
//            'com.google.mug:mug:5.4',
//            'com.typesafe:config:1.4.1',
//            'commons-cli:commons-cli:1.3.1',
//            'org.slf4j:slf4j-api:1.7.32',
            'org.slf4j:jul-to-slf4j:2.0.0-alpha4', // Guava uses java.util.logging
            'org.slf4j:slf4j-simple:2.0.0-alpha4',
//            'ch.qos.logback:logback-classic:1.2.7'
    )
//    apiOnly 'org.projectlombok:lombok:1.18.20'
    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.8.2',
            'com.google.truth:truth:1.1.3',
            'com.google.truth.extensions:truth-java8-extension:1.1.3',
            'org.mockito:mockito-core:4.1.0',
            'rip.deadcode:izvestia:0.4.0',
//            'com.google.jimfs:jimfs:1.2'
    )
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'rip.deadcode.request_viewer.MainKt'
        )
    }

    // Fat jar
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            name = 'github'
            url = 'https://maven.pkg.github.com/minebreaker/${PROJECT_NAME}'
            credentials {
                username = project.hasProperty("GITHUB_USER") ? GITHUB_USER : ''
                password = project.hasProperty("GITHUB_TOKEN") ? GITHUB_TOKEN : ''
            }
        }
    }

    publications {
        maven(MavenPublication) {
            pom {
                url = 'https://github.com/minebreaker/${PROJECT_NAME}'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
